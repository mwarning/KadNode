# Maintainer: Moritz Warning <moritzwarning@web.de>

pkgname=kadnode
pkgver=2.4.0
pkgrel=1

pkgdesc='A P2P based DNS resolver'
arch=('x86_64' 'i686')
url='http://github.com/mwarning/KadNode'
license=('MIT')
depends=('mbedtls')

backup=('etc/kadnode/kadnode.conf' 'etc/kadnode/peers.txt')

#source=(https://github.com/mwarning/KadNode/archive/v${pkgver}.tar.gz)
source=(git+https://github.com/mwarning/KadNode.git)

md5sums=('SKIP')

install="kadnode.install"

build() {
	cd ${srcdir}/KadNode
	make clean
	make FEATURES="bob tls cmd lpd nss"
}

package() {
	cd ${srcdir}/KadNode

	install -D -m755 build/kadnode ${pkgdir}/usr/bin/kadnode
	ln -s kadnode ${pkgdir}/usr/bin/kadnode-ctl

	install -D -m644 build/libnss_kadnode-2.0.so ${pkgdir}/usr/lib/libnss_kadnode.so.2

	# package systemd units go to /usr/lib/systemd, /etc is for admin override
	install -D -m644 archlinux/kadnode.service ${pkgdir}/usr/lib/systemd/system/kadnode.service
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -D -m644 misc/manpage ${pkgdir}/usr/share/man/man1/kadnode.1

	install -D -m644 misc/kadnode.conf ${pkgdir}/etc/kadnode/kadnode.conf
	install -D -m644 misc/peers.txt ${pkgdir}/etc/kadnode/peers.txt
}
